#Область СлужебныйПрограммныйИнтерфейс

Процедура ДополнитьФорму(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьПолеДляСкидки(Форма);
		ДобавитьКнопкуПересчитатьДляСкидки(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоНаФормуПередОснование(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Заголовок = "Контактное лицо";
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ИВА_КонтактноеЛицо";
	
КонецПроцедуры  


Процедура ДобавитьПолеКонтактноеЛицоНаФормуПередОснование(Форма) 
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), ,Форма.Элементы.Основание);
	ПолеВвода.Заголовок = "Контактное лицо";
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ИВА_КонтактноеЛицо";
	
КонецПроцедуры  


Процедура ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма) 
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), ,Форма.Элементы.СуммаДокумента);
	ПолеВвода.Заголовок = "Контактное лицо";
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ИВА_КонтактноеЛицо";
	
КонецПроцедуры   


Процедура ДобавитьПолеДляСкидки(Форма) 
	
	ГруппаСкидка = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа; 
	ГруппаСкидка.ОтображатьЗаголовок = Ложь;
	ГруппаСкидка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ПолеВвода = Форма.Элементы.Добавить("ИВА_СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидка);
	ПолеВвода.Заголовок = "Согласованная скидка";
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ИВА_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "ИВА_ИВА_СогласованнаяСкидкаПриИзменении");
	
КонецПроцедуры	


Процедура ДобавитьКнопкуПересчитатьДляСкидки(Форма)
	
	Команда = Форма.Команды.Добавить("ИВА_КомандаПересчитать");
	Команда.Действие = "ИВА_ДействиеПересчитать";
	
	Кнопка = Форма.Элементы.Добавить("ИВА_КнопкаПересчитать", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидка);
	Кнопка.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	Кнопка.ИмяКоманды = "ИВА_КомандаПересчитать";
	Кнопка.Заголовок = "Пересчитать";
	
КонецПроцедуры 

#КонецОбласти

